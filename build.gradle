buildscript {
    ext {
        springBootVersion = '1.4.0.M3'
    }
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/milestone" }
        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("gradle.plugin.com.gorylenko.gradle-git-properties:gradle-git-properties:1.4.16")
        classpath("org.liquibase:liquibase-gradle-plugin:1.2.1")
        classpath("com.h2database:h2:1.4.191")
        classpath("org.liquibase:liquibase-core:3.5.1")
//        classpath 'org.springframework:springloaded:1.2.6.RELEASE'
    }
}

apply plugin: 'java'
apply plugin: 'spring-boot'
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'org.liquibase.gradle'

def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}
version = "${getVersionName()}-SNAPSHOT"

jar {
    baseName = 'demo'
}
sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    maven { url "https://repo.spring.io/milestone" }
    maven { url "http://repository.primefaces.org" }
}

dependencies {
    compile('org.projectlombok:lombok')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-configuration-processor')
//    compile('org.springframework.boot:spring-boot-actuator-docs')
    compile('org.springframework.boot:spring-boot-starter-aop')
    compile('org.springframework.boot:spring-boot-starter-cache')
//    compile('org.springframework.boot:spring-boot-starter-mail')
//	compile('org.springframework.retry:spring-retry')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
    compile('org.springframework.boot:spring-boot-starter-validation')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.session:spring-session')

    compile('org.glassfish:javax.faces:2.2.13')
    compile('org.apache.tomcat.embed:tomcat-embed-jasper:8.0.33')
    compile('org.primefaces:primefaces:5.3')
    compile('org.primefaces.themes:bootstrap:1.0.10')

    compile('org.liquibase:liquibase-core')

    compile('com.google.guava:guava:19.0')

    runtime('com.h2database:h2')

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc')
}

processResources {
    from('src/main/resources') {
        include 'application.yaml'
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [version: project.version.toString()])
    }
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/changelog/db.changelog-master.xml'
            url 'jdbc:h2:tcp://localhost:9092/~/.h2/demo'
            username 'sa'
            password 'sa'
        }
    }
}