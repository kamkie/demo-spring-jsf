buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.liquibase:liquibase-core:3.5.5")
        classpath("org.postgresql:postgresql:42.2.1")
        classpath('com.moowork.gradle:gradle-node-plugin:1.1.0')
        classpath('com.toomuchcoding:uptodate-gradle-plugin:1.0.1')
    }
}

plugins {
    id 'com.palantir.git-version' version '0.10.1'
    id "org.sonarqube" version "2.6.2"
    id "com.gorylenko.gradle-git-properties" version "1.4.21"
    id "com.diffplug.gradle.spotless" version "3.10.0"
    id 'com.github.ben-manes.versions' version '0.17.0'
    id "com.github.spotbugs" version "1.6.1"
    id "org.springframework.boot" version "2.0.0.RELEASE"
    id "org.liquibase.gradle" version "1.2.4"
}

apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: 'io.spring.dependency-management'
apply plugin: "com.moowork.node"
apply plugin: 'pmd'
apply plugin: 'jacoco'
apply plugin: 'com.toomuchcoding.uptodate'

version = gitVersion()
group = 'demo'
sourceCompatibility = 1.9
targetCompatibility = 1.9

repositories {
    mavenCentral()
    maven { url "http://repository.primefaces.org" }
    maven { url "https://artifacts.alfresco.com/nexus/content/repositories/public/" }
}

ext['spring-boot.version'] = '2.0.0.RELEASE'
ext['selenium.version'] = '3.11.0'
ext['postgresql.version'] = '42.2.1'

//configurations {
//    testCompile.exclude group: "junit", module: "junit"
//    testRuntimee.exclude group: "junit", module: "junit"
//}

dependencies {
    // https://mvnrepository.com/artifact/com.google.code.findbugs/jsr305
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    compile('org.projectlombok:lombok:1.16.20')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-configuration-processor')
    compile('org.springframework.boot:spring-boot-starter-aop')
    compile('org.springframework.boot:spring-boot-starter-cache')
//    compile('org.springframework.boot:spring-boot-starter-mail')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-validation')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.session:spring-session-jdbc')
    compile('org.springframework.boot:spring-boot-starter-thymeleaf')
//    compile('org.springframework:spring-context-indexer')
//    compile('org.springframework.retry:spring-retry')

    compile("org.thymeleaf.extras:thymeleaf-extras-java8time")
    compile("org.thymeleaf.extras:thymeleaf-extras-springsecurity4")
    compile('org.liquibase:liquibase-core')
    compile('com.github.ben-manes.caffeine:caffeine')
    compile('io.micrometer:micrometer-registry-prometheus')

    compile('io.springfox:springfox-swagger2:2.8.0')
    compile('io.springfox:springfox-swagger-ui:2.8.0')
    compile('org.joinfaces:primefaces-spring-boot-starter:3.0.2')
    compile('org.primefaces.themes:bootstrap:1.0.10')
    compile('de.ruedigermoeller:fst:2.57')
//    compile 'org.zalando:logbook-spring-boot-starter:1.2.1'
//    compile("org.apache.commons:commons-pool2:2.4.2")

    runtime("org.postgresql:postgresql")

    // https://mvnrepository.com/artifact/de.appelgriepsch.logback/logback-gelf-appender
    runtime group: 'de.appelgriepsch.logback', name: 'logback-gelf-appender', version: '1.4'

    // https://mvnrepository.com/artifact/javax.xml.bind/jaxb-api
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.3.0' //required on java 9

    testCompile("org.junit.jupiter:junit-jupiter-api")
    testRuntime("org.junit.jupiter:junit-jupiter-engine")
    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.security:spring-security-test')
    testCompile("io.github.bonigarcia:webdrivermanager:2.1.0")
    testCompile("io.github.bonigarcia:selenium-jupiter:2.0.0")
    testCompile("org.seleniumhq.selenium:selenium-chrome-driver")
    testCompile("org.seleniumhq.selenium:selenium-support")
    testCompile("org.testcontainers:postgresql:1.6.0")
    testCompile("org.glassfish.jersey.inject:jersey-hk2:2.26")
    testCompile group: 'javax.activation', name: 'activation', version: '1.1.1'
}

sourceSets {
    main {
        resources.srcDir "${buildDir}/generated/"
    }
}

springBoot {
    buildInfo()
}

bootRun {
    systemProperty 'spring.output.ansi.enabled', "always"
    systemProperty 'spring.profiles.active', "graylog"
    classpath = getProject().files(sourceSets.main.getResources().getSrcDirs(), classpath)
}

bootJar {
    classifier = 'boot'
}

task springConfiguration(type: Copy) {
    inputs.files("$buildDir/classes/java/main")
    outputs.dir("$buildDir/generated")
    from file("$buildDir/classes/java/main/META-INF/")
    into file("$buildDir/generated/META-INF/")
    doLast {
        file("$buildDir/classes/java/main/META-INF").deleteDir()
    }
}
classes.dependsOn springConfiguration
compileJava.dependsOn processResources
generateGitProperties.mustRunAfter processResources
bootBuildInfo.mustRunAfter processResources
springConfiguration.mustRunAfter compileJava
build.dependsOn jacocoTestReport


liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/changelog/db.changelog-master.xml'
            url 'jdbc:postgresql://localhost:5432/spring-demo'
            username 'dev'
            password 'dev'
        }
    }
}

jacoco.toolVersion = "0.8.0"
spotbugs.toolVersion = "3.1.2"
pmd.toolVersion = "6.1.0"

sonarqube {
    properties {
        property "sonar.host.url", System.getenv("SONAR_URL") ?: "http://127.0.0.1:9000"
        property "sonar.projectName", "spring jsf project"
        property "sonar.projectKey", "${project.group}:${project.name}"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.exclusions", ""
    }
}

task h2Tcp(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "org.h2.tools.Console"
    args["-tcp"]
    classpath = buildscript.configurations.classpath
    setDependsOn([])
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    inputs.files("src/main/resources/static/javascript")
    outputs.dir("build/resources/main/static/javascript")
    script = project.file('node_modules/webpack/bin/webpack.js')
}

task webpackWatch(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/webpack/bin/webpack.js')
    args = ['--watch', '--display-error-details']
}
processResources.dependsOn 'webpack'

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

spotless {
    java {
        eclipse().configFile 'spotless.eclipseformat.xml'    // XML file dumped out by the Eclipse formatter
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()

        // Eclipse formatter puts excess whitespace after lambda blocks
        //    funcThatTakesLambdas(x -> {} , y -> {} )	// what Eclipse does
        //    funcThatTakesLambdas(x -> {}, y -> {})	// what I wish Eclipse did
        custom 'Lambda fix', { it.replace('} )', '})').replace('} ,', '},') }

        // Eclipse formatter screws up long literals with underscores inside of annotations (see issue #14)
        //    @Max(value = 9_999_999 L) // what Eclipse does
        //    @Max(value = 9_999_999L)  // what I wish Eclipse did
        custom 'Long literal fix', { it.replaceAll('([0-9_]+) [Ll]', '$1L') }
    }
    format 'misc', {
        target fileTree('.') {
            include '.gitignore', '**/.gitignore', 'build.gradle', '*.md', 'src/**/*.md', '*.sh', 'src/**/*.sh'
            exclude 'node_modules/**', 'out/**'
        }
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
    testLogging {
        // set options for log level LIFECYCLE
        events "passed", "skipped", "failed", /*"standardOut",*/ "standardError"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
    distributionType = org.gradle.api.tasks.wrapper.Wrapper.DistributionType.ALL
}
