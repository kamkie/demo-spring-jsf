plugins {
    id "com.palantir.git-version" version "0.12.0-rc2"
    id "org.sonarqube" version "2.7"
    id "com.gorylenko.gradle-git-properties" version "2.0.0"
    id "com.diffplug.gradle.spotless" version "3.20.0"
    id "com.github.ben-manes.versions" version "0.21.0"
    id "com.github.spotbugs" version "1.6.9"
    id "org.springframework.boot" version "2.1.3.RELEASE"
    id "org.liquibase.gradle" version "2.0.1"
    id "org.asciidoctor.convert" version "1.5.3"
    id "com.moowork.node" version "1.3.1"
    id "com.ofg.uptodate" version "1.6.3"
    id 'com.adarshr.test-logger' version '1.6.0'
}

apply plugin: "java"
apply plugin: "idea"
apply plugin: "org.springframework.boot"
apply plugin: "com.moowork.node"
apply plugin: "pmd"
apply plugin: "jacoco"

def javaVersion = JavaVersion.VERSION_11
def springBootVersion = "2.1.3.RELEASE"
def springfoxVersion = "2.9.2"
def joinfacesVersion = "4.0.3"

version = gitVersion()
group = "demo"
sourceCompatibility = javaVersion
targetCompatibility = javaVersion

repositories {
    mavenCentral()
    maven { url "http://repository.primefaces.org" }
}

dependencies {
    implementation(platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))
    annotationProcessor(platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))
    testAnnotationProcessor(platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))
    asciidoctor(platform("org.springframework.boot:spring-boot-dependencies:$springBootVersion"))

    annotationProcessor("org.projectlombok:lombok")
    testAnnotationProcessor("org.projectlombok:lombok")
    annotationProcessor("org.springframework.boot:spring-boot-configuration-processor")
//    annotationProcessor("org.springframework:spring-context-indexer")

    asciidoctor "org.springframework.restdocs:spring-restdocs-asciidoctor"

    liquibaseRuntime("org.postgresql:postgresql")
    liquibaseRuntime("org.liquibase:liquibase-core")

    // https://mvnrepository.com/artifact/com.google.code.findbugs/jsr305
    implementation("com.google.code.findbugs:jsr305:3.0.2")
    implementation("org.projectlombok:lombok")

    implementation("org.springframework.boot:spring-boot-devtools")
    implementation("org.springframework.boot:spring-boot-starter-actuator")
    implementation("org.springframework.boot:spring-boot-starter-aop")
    implementation("org.springframework.boot:spring-boot-starter-cache")
//    implementation("org.springframework.boot:spring-boot-starter-mail")
    implementation("org.springframework.boot:spring-boot-starter-security")
    implementation("org.springframework.boot:spring-boot-starter-web")
    implementation("org.springframework.boot:spring-boot-starter-validation")
    implementation("org.springframework.boot:spring-boot-starter-data-jpa")
    implementation("org.springframework.session:spring-session-jdbc")
    implementation("org.springframework.boot:spring-boot-starter-thymeleaf")
//    implementation("org.springframework.retry:spring-retry")

    implementation("org.thymeleaf.extras:thymeleaf-extras-java8time")
    implementation("org.thymeleaf.extras:thymeleaf-extras-springsecurity5")
    implementation("org.liquibase:liquibase-core")
    implementation("com.github.ben-manes.caffeine:caffeine:2.7.0")
    implementation("io.micrometer:micrometer-registry-prometheus")

    implementation("io.springfox:springfox-swagger2:$springfoxVersion")
    implementation("io.springfox:springfox-swagger-ui:$springfoxVersion")
    implementation("org.joinfaces:primefaces-spring-boot-starter:$joinfacesVersion")
    implementation("com.google.guava:guava:27.1-jre")
    implementation("org.primefaces.themes:bootstrap:1.0.10")
    implementation("de.ruedigermoeller:fst:2.57")
//    implementation "org.zalando:logbook-spring-boot-starter:1.2.1"
//    implementation("org.apache.commons:commons-pool2:2.4.2")

    implementation("org.postgresql:postgresql")

    // https://mvnrepository.com/artifact/de.appelgriepsch.logback/logback-gelf-appender
    implementation group: "de.appelgriepsch.logback", name: "logback-gelf-appender", version: "1.5"

    // https://mvnrepository.com/artifact/javax.xml.bind/jaxb-api
    implementation group: "javax.xml.bind", name: "jaxb-api", version: "2.4.0-b180830.0359"
    // https://mvnrepository.com/artifact/javax.enterprise/cdi-api
    implementation group: "javax.enterprise", name: "cdi-api", version: "2.0.SP1"

    // https://mvnrepository.com/artifact/org.junit.jupiter/junit-jupiter
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '5.4.0'
    testImplementation("org.springframework.boot:spring-boot-starter-test")
    testImplementation("org.springframework.security:spring-security-test")
    testImplementation("org.springframework.restdocs:spring-restdocs-mockmvc")
    testImplementation("io.github.bonigarcia:selenium-jupiter:3.1.1")
    testImplementation("org.seleniumhq.selenium:selenium-chrome-driver")
    testImplementation("org.seleniumhq.selenium:selenium-support")
    testImplementation("org.testcontainers:postgresql:1.10.7") {
        exclude group: "log4j", module: "log4j"
    }
    testImplementation("org.glassfish.jersey.bundles.repackaged:jersey-guava:2.26-b03")
    testImplementation("org.glassfish.jersey.inject:jersey-hk2:2.28")
}

sourceSets {
    main {
        resources.srcDir "${buildDir}/generated/"
    }
}

idea {
    module {
        inheritOutputDirs = false
        outputDir = file("${buildDir}/resources/main/")
    }
}

springBoot {
    buildInfo()
}

bootRun {
    systemProperty "spring.output.ansi.enabled", "always"
    systemProperty "spring.profiles.active", "graylog"
    classpath = getProject().files(sourceSets.main.getResources().getSrcDirs(), classpath)
}

bootJar {
    archiveClassifier.set("boot")
    dependsOn asciidoctor
}

test {
    outputs.dir("build/generated-snippets")
}

asciidoctor {
    mustRunAfter test
    inputs.dir("build/generated-snippets")
    outputs.dir("build/resources/main")
    doLast {
        copy {
            from "build/asciidoc/html5"
            into "build/resources/main/static/docs"
            include "index.html"
        }
    }
}

task springConfiguration(type: Copy) {
    inputs.files("$buildDir/classes/java/main")
    outputs.dir("$buildDir/generated")
    from file("$buildDir/classes/java/main/META-INF/")
    into file("$buildDir/generated/META-INF/")
    doLast {
        file("$buildDir/classes/java/main/META-INF").deleteDir()
    }
}
classes.dependsOn springConfiguration
compileJava.dependsOn processResources
generateGitProperties.mustRunAfter processResources
bootBuildInfo.mustRunAfter processResources
springConfiguration.mustRunAfter compileJava
build.dependsOn jacocoTestReport


liquibase {
    activities {
        main {
            changeLogFile "src/main/resources/db/changelog/db.changelog-master.xml"
            url "jdbc:postgresql://localhost:5432/spring-demo"
            username "dev"
            password "dev"
        }
    }
}

jacoco.toolVersion = "0.8.3"
spotbugs.toolVersion = "3.1.11"
pmd {
    toolVersion = "6.8.0"
    ruleSets = ["pmd.ruleset.xml"]
}

sonarqube {
    properties {
        property "sonar.host.url", System.getenv("SONAR_URL") ?: "http://127.0.0.1:9000"
        property "sonar.projectName", "spring jsf project"
        property "sonar.projectKey", "${project.group}:${project.name}"
        property "sonar.jacoco.reportPaths", "${project.buildDir}/jacoco/test.exec"
        property "sonar.exclusions", ""
    }
}
tasks.sonarqube.setDependsOn([])


task h2Tcp(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "org.h2.tools.Console"
    args["-tcp"]
    classpath = buildscript.configurations.classpath
    setDependsOn([])
}

task webpack(type: NodeTask, dependsOn: "npmInstall") {
    inputs.files("src/main/resources/static/javascript")
    outputs.dir("build/resources/main/static/javascript")
    script = project.file("node_modules/webpack/bin/webpack.js")
}

task webpackWatch(type: NodeTask, dependsOn: "npmInstall") {
    script = project.file("node_modules/webpack/bin/webpack.js")
    args = ["--watch", "--display-error-details"]
}
processResources.dependsOn "webpack"

compileJava {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    options.encoding = "UTF-8"
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

spotless {
    java {
        eclipse().configFile "spotless.eclipseformat.xml"    // XML file dumped out by the Eclipse formatter
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()

        // Eclipse formatter puts excess whitespace after lambda blocks
        //    funcThatTakesLambdas(x -> {} , y -> {} )	// what Eclipse does
        //    funcThatTakesLambdas(x -> {}, y -> {})	// what I wish Eclipse did
        custom "Lambda fix", { it.replace("} )", "})").replace("} ,", "},") }

        // Eclipse formatter screws up long literals with underscores inside of annotations (see issue #14)
        //    @Max(value = 9_999_999 L) // what Eclipse does
        //    @Max(value = 9_999_999L)  // what I wish Eclipse did
        custom "Long literal fix", { it.replaceAll("([0-9_]+) [Ll]", "\$1L") }
    }
    format "misc", {
        target fileTree(".") {
            include ".gitignore", "**/.gitignore", "build.gradle", "*.md", "src/**/*.md", "*.sh", "src/**/*.sh"
            exclude "node_modules/**", "out/**"
        }
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
//    testLogging {
//        // set options for log level LIFECYCLE
//        events "passed", "skipped", "failed", /*"standardOut",*/ "standardError"
//        showExceptions true
//        exceptionFormat "full"
//        showCauses true
//        showStackTraces true
//
//        afterSuite { desc, result ->
//            if (!desc.parent) { // will match the outermost suite
//                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
//                def startItem = "|  ", endItem = "  |"
//                def repeatLength = startItem.length() + output.length() + endItem.length()
//                println("\n" + ("-" * repeatLength) + "\n" + startItem + output + endItem + "\n" + ("-" * repeatLength))
//            }
//        }
//    }
}

wrapper {
    gradleVersion = "5.3"
    distributionType = Wrapper.DistributionType.ALL
}
