buildscript {
    ext {
        springBootVersion = '1.5.6.RELEASE'
        hibernateVersion = '5.2.10.Final'
    }
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("org.liquibase:liquibase-gradle-plugin:1.2.4")
        classpath("com.h2database:h2:1.4.196")
        classpath("org.liquibase:liquibase-core:3.5.3")
        classpath 'com.moowork.gradle:gradle-node-plugin:1.1.0'
        classpath 'com.toomuchcoding:uptodate-gradle-plugin:1.0.1'
        classpath "org.hibernate:hibernate-gradle-plugin:${hibernateVersion}"
    }
}

plugins {
    id 'com.palantir.git-version' version '0.5.3'
    id "org.sonarqube" version "2.5"
    id "com.gorylenko.gradle-git-properties" version "1.4.17"
    id "com.diffplug.gradle.spotless" version "2.4.1"
    id 'com.github.ben-manes.versions' version '0.15.0'
}

//apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'java'
apply plugin: 'org.springframework.boot'
apply plugin: "com.moowork.node"
apply plugin: 'com.gorylenko.gradle-git-properties'
apply plugin: 'org.liquibase.gradle'
apply plugin: 'pmd'
//apply plugin: 'findbugs'
apply plugin: 'jacoco'
apply plugin: 'org.sonarqube'
apply plugin: 'com.diffplug.gradle.spotless'
apply plugin: 'com.toomuchcoding.uptodate'
apply plugin: 'org.hibernate.orm'

version = gitVersion()
group = 'demo'
sourceCompatibility = 1.8
targetCompatibility = 1.8
sourceSets.main.output.classesDir = new File(buildDir, "classes/main")

repositories {
    mavenCentral()
    maven { url "http://repository.primefaces.org" }
    maven { url "https://artifacts.alfresco.com/nexus/content/repositories/public/" }
}

dependencyManagement {
    imports {
//        mavenBom 'org.springframework.cloud:spring-cloud-sleuth:1.1.0.RELEASE'
    }
}

ext['thymeleaf.version'] = '3.0.3.RELEASE'
ext['selenium.version'] = '3.0.1'
ext['hibernate.version'] = '5.2.10.Final'

dependencies {
    // https://mvnrepository.com/artifact/com.google.code.findbugs/jsr305
    compile group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.2'

    compile('org.projectlombok:lombok:1.16.18')
    compile('org.springframework.boot:spring-boot-devtools')
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-configuration-processor')
    compile('org.springframework.boot:spring-boot-starter-aop')
    compile('org.springframework.boot:spring-boot-starter-cache')
//    compile('org.springframework.boot:spring-boot-starter-mail')
    compile('org.springframework.boot:spring-boot-starter-security')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-validation')
    compile('org.springframework.boot:spring-boot-starter-data-jpa')
    compile('org.springframework.session:spring-session')
//    compile 'org.springframework.cloud:spring-cloud-starter-sleuth'

    compile('org.springframework.boot:spring-boot-starter-thymeleaf') {
        exclude group: 'nz.net.ultraq.thymeleaf', module: 'thymeleaf-layout-dialect'
    }
    compile("org.thymeleaf.extras:thymeleaf-extras-springsecurity4:3.0.2.RELEASE")
    compile("org.thymeleaf.extras:thymeleaf-extras-java8time:3.0.0.RELEASE")

    compile('io.springfox:springfox-swagger2:2.7.0')
    compile('io.springfox:springfox-swagger-ui:2.7.0')

    // https://mvnrepository.com/artifact/org.aspectj/aspectjweaver
    compile group: 'org.aspectj', name: 'aspectjweaver', version: '1.9.0.BETA-5'

    compile("com.ryantenney.metrics:metrics-spring:3.1.3")

    compile('de.ruedigermoeller:fst:2.52')

    compile('org.joinfaces:jsf-spring-boot-starter:2.4.0')
    compile group: 'org.primefaces.themes', name: 'bootstrap', version: '1.0.10'

//    compile 'org.zalando:logbook-spring-boot-starter:1.2.1'


    compile('org.liquibase:liquibase-core')

    compile 'com.github.ben-manes.caffeine:caffeine:2.5.3' //newer version than in spring boot

//    compile("org.apache.commons:commons-pool2:2.4.2")
//    compile('org.springframework.retry:spring-retry')

    runtime('com.h2database:h2:1.4.196')

    // https://mvnrepository.com/artifact/javax.xml.bind/jaxb-api
    compile group: 'javax.xml.bind', name: 'jaxb-api', version: '2.2.12'

    testCompile('org.springframework.boot:spring-boot-starter-test')
    testCompile('org.springframework.restdocs:spring-restdocs-mockmvc:1.2.1.RELEASE')

    testCompile("io.github.bonigarcia:webdrivermanager:1.7.1")
    testCompile("org.seleniumhq.selenium:selenium-chrome-driver:3.4.0")//newer version than in spring boot
    testCompile("org.seleniumhq.selenium:selenium-support:3.4.0")//newer version than in spring boot

    testCompile("org.powermock:powermock-api-mockito:1.7.0")
    testCompile("org.powermock:powermock-module-junit4:1.7.0")
}

processResources {
    from('src/main/resources') {
        include 'application.yaml'
        filter(org.apache.tools.ant.filters.ReplaceTokens, tokens: [version: project.version.toString()])
    }
}

liquibase {
    activities {
        main {
            changeLogFile 'src/main/resources/db/changelog/db.changelog-master.xml'
            url 'jdbc:h2:file:~/.demo/demo-jsf;AUTO_SERVER=TRUE'
            username 'sa'
            password 'sa'
        }
    }
}

bootRun {
    addResources = true
    systemProperties["spring.output.ansi.enabled"] = "always"
}

hibernate {
    enhance {
        enableLazyInitialization= true
        enableDirtyTracking = true
        enableAssociationManagement = true
    }
}

sonarqube {
    properties {
        property "sonar.host.url", System.getenv("SONAR_URL") ?: "http://127.0.0.1:9000"
        property "sonar.projectName", "spring jsf project"
        property "sonar.projectKey", "${project.group}:${project.name}"
        property "sonar.jacoco.reportPath", "${project.buildDir}/jacoco/test.exec"
        property "sonar.exclusions", ""
    }
}

task h2Tcp(type: JavaExec) {
    classpath sourceSets.main.runtimeClasspath
    main = "org.h2.tools.Console"
    args["-tcp"]
    classpath = buildscript.configurations.classpath
    setDependsOn([])
}

task buildInfoProperties {
    doLast {
        def file = new File(project.buildDir, 'resources/main/META-INF/build-info.properties')
        if (!file.parentFile.exists()) {
            file.parentFile.mkdirs()
        }
        if (file.exists()) {
            assert file.delete()
        }
        assert file.createNewFile()
        logger.info "writing to [${file}]"
        def map = [
                "build.version"            : version,
                "build.spring.boot.version": springBootVersion,
                "build.artifact"           : project.name,
                "build.group"              : project.group,
                "build.name"               : rootProject.name,
                "build.time"               : new Date().time,
        ]

        file.withWriter('UTF-8') { w ->
            map.each { key, value ->
                w.writeLine "$key=$value"
            }
        }
    }
}

task webpack(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/webpack/bin/webpack.js')
}

task webpackWatch(type: NodeTask, dependsOn: 'npmInstall') {
    script = project.file('node_modules/webpack/bin/webpack.js')
    args = ['--watch', '--display-error-details']
}
processResources.dependsOn 'webpack'

clean.delete << file('src/main/webapp/dist')


processResources.dependsOn << buildInfoProperties

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

compileTestJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

spotless {
    java {
        eclipseFormatFile 'spotless.eclipseformat.xml'    // XML file dumped out by the Eclipse formatter
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()

        // Eclipse formatter puts excess whitespace after lambda blocks
        //    funcThatTakesLambdas(x -> {} , y -> {} )	// what Eclipse does
        //    funcThatTakesLambdas(x -> {}, y -> {})	// what I wish Eclipse did
        custom 'Lambda fix', { it.replace('} )', '})').replace('} ,', '},') }

        // Eclipse formatter screws up long literals with underscores inside of annotations (see issue #14)
        //    @Max(value = 9_999_999 L) // what Eclipse does
        //    @Max(value = 9_999_999L)  // what I wish Eclipse did
        custom 'Long literal fix', { it.replaceAll('([0-9_]+) [Ll]', '$1L') }
    }
    format 'misc', {
        target fileTree('.') {
            include '.gitignore', '**/.gitignore', 'build.gradle', '*.md', 'src/**/*.md', '*.sh', 'src/**/*.sh'
            exclude 'node_modules/**', 'out/**'
        }
        indentWithSpaces()
        trimTrailingWhitespace()
        endWithNewline()
    }
}

tasks.withType(Test) {
    testLogging {
        // set options for log level LIFECYCLE
        events "passed", "skipped", "failed", "standardOut"
        showExceptions true
        exceptionFormat "full"
        showCauses true
        showStackTraces true

        // set options for log level DEBUG and INFO
        debug {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
        }
        info.events = debug.events
        info.exceptionFormat = debug.exceptionFormat

        afterSuite { desc, result ->
            if (!desc.parent) { // will match the outermost suite
                def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} successes, ${result.failedTestCount} failures, ${result.skippedTestCount} skipped)"
                def startItem = '|  ', endItem = '  |'
                def repeatLength = startItem.length() + output.length() + endItem.length()
                println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))
            }
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.1'
    distributionType = org.gradle.api.tasks.wrapper.Wrapper.DistributionType.ALL
}
